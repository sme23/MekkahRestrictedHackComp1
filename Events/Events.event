
EventPointerTable(0x7,Chapter0)

Chapter0:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN PlayerUnits PlayerUnits
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

PlayerUnits:
UNIT Marth MarthLord Marth Level(15,Ally,0) [2,0] 0x0 0x0 0x1 REDA3R3 [Rapier,SteelSword,Elixir] NoAI
UNIT Caeda Falcoknight Marth Level(1,Ally,0) [2,0] 0x0 0x0 0x1 REDA1R3 [WingSpear,SteelLance,DelphiShield] NoAI
UNIT Draug General Marth Level(1,Ally,0) [2,0] 0x0 0x0 0x1 REDA3R2 [SilverLance,Javelin,HoplonGuard] NoAI
UNIT Norne Sniper_F Marth Level(1,Ally,0) [2,0] 0x0 0x0 0x1 REDA1R2 [KillerBow,SilverBow] NoAI
UNIT Wrys Bishop Marth Level(1,Ally,0) [2,0] 0x0 0x0 0x1 REDA2R2 [EllightTome,Physic,Mend] NoAI
UNIT Merric Sage Marth Level(1,Ally,0) [2,0] 0x0 0x0 0x1 REDA3R1 [ExcaliburTome,ElwindTome] NoAI
UNIT Darros Berserker Marth Level(1,Ally,0) [2,0] 0x0 0x0 0x1 REDA2R3 [KillerAxe,SteelAxe] NoAI
UNIT Linde Sage_F Marth Level(1,Ally,0) [2,0] 0x0 0x0 0x1 REDA1R1 [AuraTome,ArcfireTome] NoAI
UNIT Rickard Rogue Marth Level(5,Ally,0) [2,0] 0x0 0x0 0x1 REDA2R1 [IronSword] NoAI
UNIT 

EnemyUnits:
UNIT Gharnef Druid Gharnef Level(10,Enemy,0) [9,8] 0x0 0x0 0x0 0x1 [Imhullu] AttackInRangeAI
UNIT DolhrGeneric Hero Gharnef Level(3,Enemy,1) [10,2] 0x0 0x0 0x0 0x1 [SteelSword,HandAxe] AttackInRangeAI
UNIT DolhrGeneric Hero Gharnef Level(3,Enemy,1) [3,6] 0x0 0x0 0x0 0x1 [SteelAxe,SilverSword] AttackInRangeAI
UNIT DolhrGeneric Hero Gharnef Level(3,Enemy,1) [0,8] 0x2 0x0 0x0 0x1 [Tomahawk] AttackInRangeAI
UNIT DolhrGeneric Paladin Gharnef Level(4,Enemy,1) [13,1] 0x0 0x0 0x0 0x1 [Javelin] AttackInRangeAI
UNIT DolhrGeneric Paladin Gharnef Level(3,Enemy,1) [11,5] 0x2 0x0 0x0 0x1 [ShortSpear] AttackInRangeAI
UNIT DolhrGeneric General Gharnef Level(5,Enemy,1) [8,10] 0x0 0x0 0x0 0x1 [Spear] AttackInRangeAI
UNIT DolhrGeneric Swordmaster_F Gharnef Level(4,Enemy,1) [14,9] 0x2 0x0 0x0 0x1 [KillingEdge] AttackInRangeAI
UNIT DolhrGeneric Warrior Gharnef Level(3,Enemy,1) [7,0] 0x0 0x0 0x0 0x1 [SilverAxe,KillerBow] AttackInRangeAI
UNIT DolhrGeneric Warrior Gharnef Level(4,Enemy,1) [9,4] 0x2 0x0 0x0 0x1 [BraveAxe] AttackInRangeAI
UNIT DolhrGeneric Sage Gharnef Level(5,Enemy,1) [6,6] 0x2 0x0 0x0 0x1 [BolganoneTome] AttackInRangeAI
UNIT DolhrGeneric Sage Gharnef Level(6,Enemy,1) [5,9] 0x0 0x0 0x0 0x1 [ElthunderTome,Physic] AttackInRangeAI
UNIT DolhrGeneric Sage Gharnef Level(6,Enemy,1) [14,4] 0x2 0x0 0x0 0x1 [ThoronTome] AttackInRangeAI
UNIT DolhrGeneric Sage Gharnef Level(5,Enemy,1) [12,6] 0x0 0x0 0x0 0x1 [ElwindTome,ElfireTome] AttackInRangeAI
UNIT DolhrGeneric Bishop Gharnef Level(6,Enemy,1) [11,0] 0x0 0x0 0x0 0x1 [EllightTome,Physic] AttackInRangeAI
UNIT DolhrGeneric Bishop Gharnef Level(7,Enemy,1) [5,7] 0x0 0x0 0x0 0x1 [LightTome,Physic] AttackInRangeAI
UNIT 

TurnBasedEvents:
TurnEventPlayer(0,EndingScene,31)
TurnEventPlayer(0,RandomTurnEvents,3,30)
TurnEventPlayer(0,EndIfNormal,26)
TurnEventPlayer(0,EndIfHard,21)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Seize(2,0)
Chest(BraveLance,9,11)
Chest(BraveBow,12,10)
Chest(Elixir,14,11)
END_MAIN

MiscBasedEvents:
AFEV 0 EndingScene 0x66
END_MAIN

Dunno:
//DO NOT TOUCH
WORD $00

Tutorial:
//DO NOT TOUCH
WORD $00

TrapData:
END_MAIN

CutsceneMars:
UNIT Joshua MarthLord 0 Level(0,Ally,0) [5,9] 0x0 0x0 0x1 REDA10R3 NoItems NoAI
UNIT

BeginningScene:

CHECK_TUTORIAL
SVAL 1 1
BNE 200 1 0xC
    ENUT 0xB0
	UnlockAllGuideEntries
LABEL 200

ENUT 0x22

MUSC 0x2E
LOMA 2 //cutscene map
FADU 10
LOAD1 0x1 CutsceneMars
ENUN
CUMO [10,3]
STAL 40
CURE
SetBackground(HouseBG)
Text(IntroCantWinHouse)
FADI 10
DISA Joshua
LOMA 0 //actual map
MUSC 0x1B
LOAD1 0x1 EnemyUnits
ENUN
FADU 10
LOAD1 0x1 PlayerUnits
ENUN
TEXTSTART
TEXTSHOW IntroMapStart
TEXTEND
CUMO [9,8]
STAL 40
CURE
TEXTCONT
TEXTEND
REMA

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationStart
TEXTEND

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationFunds
TEXTEND

CHECK_TUTORIAL
SVAL 1 1
BNE 201 1 0xC

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationDeathsCasual
TEXTEND

GOTO 201

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationDeaths
TEXTEND

LABEL 201

CHECK_HARD
SVAL 1 1
BNE 202 1 0xC

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationHardBonus
TEXTEND

LABEL 202

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationTurnEvents
TEXTEND

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationEscape1
TEXTEND

CUMO [2,0]
STAL 60
CURE

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationEscape2
TEXTEND

CHECK_TUTORIAL
SVAL 1 1
BNE 701 1 0xC

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationForcedEndCasual
TEXTEND
GOTO 703

LABEL 701

CHECK_HARD
SVAL 1 1
BNE 702 1 0xC

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationForcedEndHard
TEXTEND
GOTO 703

LABEL 702

CHECK_HARD
SVAL 1 1
BEQ 703 1 0xC

CHECK_TUTORIAL
SVAL 1 1
BEQ 703 1 0xC

CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW IntroExplanationForcedEndNormal
TEXTEND
GOTO 703

LABEL 703

NoFade
ENDA

EndingScene:
CHECK_EVENTID 176
SVAL 1 1
BNE 301 1 0xC
  MUSC 0x32
  SetBackground(HouseBG)
  Text(EndingGharnefDies)
  GOTO 302
LABEL 301
  MUSC 0x31
  SetBackground(HouseBG)
  Text(EndingGharnefLives)
LABEL 302

MUSC 0x3C
Text(MoneymanRankingBegin)
CALL ScoreCalculation

MUSC 0x3A

SVAL 1 0
BNE 303 1 5
  Text(MoneymanRankingPoor)
  GOTO 305
LABEL 303
SVAL 1 1
BNE 304 1 5
  Text(MoneymanRankingGood)
  GOTO 305
LABEL 304
SVAL 1 2
BNE 305 1 5
  Text(MoneymanRankingBest)
LABEL 305

ASMC 0x85C71 0x381F5 0x37CF9 //show records screen
STAL 120
EVBIT_F 0x2
MNTS 0x1 //return to title screen
NoFade
ENDA


RandomTurnEvents:
RANDOMNUMBER 100

SVAL 1 60
BLE 100 1 0xC

  GOTO 103

LABEL 100
SVAL 1 70
BLE 101 1 0xC

  CHECK_EVENTID 236
  SVAL 1 1
  BNE 104 1 0xC

    WEA1 6
    ENUT 236
    GOTO 103

  LABEL 104
  WEA1 0
  ENUF 236
  GOTO 103

LABEL 101
SVAL 1 80
BLE 102 1 0xC

  CHECK_EVENTID 237
  SVAL 1 1
  BNE 105 1 0xC
  
    VCWF 5
    ENUT 237
    GOTO 103
  
  LABEL 105
  VCWF 0
  ENUF 237
  GOTO 103
  
LABEL 102
SVAL 1 100
BLE 103 1 0xC

  RANDOMNUMBER 1
  SVAL 1 1
  BNE 106 1 0xC
  
    LOAD1 0x1 SoldierReinforcements
    ENUN
    GOTO 103
  
  LABEL 106
  LOAD1 0x1 BanditReinforcements
  ENUN
  GOTO 103

LABEL 103
NoFade
ENDA

SoldierReinforcements:
UNIT DolhrGeneric Hero Gharnef Level(3,Enemy,1) [12,0] 0x0 0x0 0x0 0x1 [HandAxe] NoAI
UNIT DolhrGeneric Sage Gharnef Level(3,Enemy,1) [0,11] 0x0 0x0 0x0 0x1 [BoltingTome] NoAI
UNIT DolhrGeneric Assassin Gharnef Level(3,Enemy,1) [14,5] 0x0 0x0 0x0 0x1 [SteelSword] NoAI
UNIT

BanditReinforcements:
UNIT BanditGeneric Rogue 0 Level(3,Enemy,1) [3,11] 0x0 0x0 0x0 0x1 [IronSword] [0x0,0x4,0x0,0x0]
UNIT BanditGeneric Brigand 0 Level(18,Enemy,1) [5,11] 0x2 0x0 0x0 0x1 [SilverAxe] [0x0,0xE,0x0,0x0]
UNIT BanditGeneric Brigand 0 Level(19,Enemy,1) [4,11] 0x0 0x0 0x0 0x1 [Tomahawk] [0x0,0xE,0x0,0x0]
UNIT


ScoreCalculation:
ASMC CallFundsEvalASMC|1 //returns rank from 0 to 5 in memory slot 1
SADD 0x12
COUNTER_SET 2 (-1)

CHECK_EVENTID 176
SVAL 1 1
BNE 8 1 0xC
  
  COUNTER_INC 2
  COUNTER_INC 2
  
LABEL 8

CHECK_TUTORIAL
SVAL 1 1
BEQ 9 1 0xC

  CHECK_EVENTID 120
  SVAL 1 1
  BNE 10 1 0xC
    COUNTER_DEC 2
  LABEL 10
  
  CHECK_EVENTID 121
  SVAL 1 1
  BNE 11 1 0xC
    COUNTER_DEC 2
  LABEL 11
  
  CHECK_EVENTID 122
  SVAL 1 1
  BNE 12 1 0xC
    COUNTER_DEC 2
  LABEL 12
  
  CHECK_EVENTID 123
  SVAL 1 1
  BNE 13 1 0xC
    COUNTER_DEC 2
  LABEL 13
  
    CHECK_EVENTID 124
  SVAL 1 1
  BNE 14 1 0xC
    COUNTER_DEC 2
  LABEL 14
  
  CHECK_EVENTID 125
  SVAL 1 1
  BNE 15 1 0xC
    COUNTER_DEC 2
  LABEL 15
  
  CHECK_EVENTID 126
  SVAL 1 1
  BNE 16 1 0xC
    COUNTER_DEC 2
  LABEL 16
  
  CHECK_EVENTID 131
  SVAL 1 1
  BNE 17 1 0xC
    COUNTER_DEC 2
  LABEL 17

LABEL 9

CHECK_HARD
SVAL 1 1
BNE 610 1 0xC

  COUNTER_INC 2
  
LABEL 610

COUNTER_CHECK 2
SVAL 1 7
BGT 18 1 0xC
  SVAL 5 0
LABEL 18
SVAL 1 14
BGT 19 1 0xC
  SVAL 5 1
LABEL 19
SVAL 1 15
BNE 20 1 0xC
  SVAL 5 2
LABEL 20
ENDA


EndIfHard:
CHECK_HARD
SVAL 1 1
BNE 1 1 0xC
CALL EndingScene
LABEL 1
NoFade
ENDA


EndIfNormal:
CHECK_TUTORIAL
SVAL 1 1
BEQ 1 1 0xC
CHECK_HARD
SVAL 1 1
BEQ 1 1 0xC
CALL EndingScene
LABEL 1
NoFade
ENDA













